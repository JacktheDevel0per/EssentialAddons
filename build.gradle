plugins {
	id "com.replaymod.preprocess" version "897c4096e2"
	id 'fabric-loom' version '1.0-SNAPSHOT' apply false
	id 'io.github.juuxel.loom-quiltflower' version '1.7.0' apply false
}

preprocess {
	def mc117 = createNode("1.17.1", 1_17_01, "yarn")
	def mc118 = createNode("1.18.2", 1_18_02, "yarn")
	def mc1192 = createNode("1.19.2", 1_19_02, "yarn")
	def mc1194 = createNode("1.19.4", 1_19_04, "yarn")
	mc1194.link(mc1192, file("versions/mapping-1.19.4-1.19.2.txt"))
	mc1192.link(mc118, file("versions/mapping-1.19.2-1.18.2.txt"))
	mc118.link(mc117, file("versions/mapping-1.18.2-1.17.1.txt"))
}


task buildAndGather {
	subprojects {
		dependsOn project.name + ":build"
	}
	doFirst {
		println "Gathering builds"
		copy {
			subprojects {
				def libDir = project.projectDir.toPath().resolve("build/libs")
				from(libDir) {
					include "*.jar"
					exclude "*-dev.jar", "*-sources.jar", "*-all.jar"
				}
				into "build/libs/"
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
